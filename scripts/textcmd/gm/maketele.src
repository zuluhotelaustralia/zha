//# 2-way teleporter
//Item 0x6203
//{
//	Name			dungtele
//	Graphic		 1FD4
//	WalkOnScript	makedtele
//	Movable		 0
//	Invisible	   1
//	SaveOnExit		1
//}
//
// *  - Makes the gates invisible after a period of time has elapsed.

use os;
use uo;

include "include/client";
include "include/objtype";
include "include/cmdlevel_check";


program make_a_tele( seer )
	if (!checkStaff(seer, "maketele", CMDLEVEL_GAME_MASTER))
		return 0;
	endif
	

	SendSysMessage( seer, "Select a rune to gate from." );

	var rune := Target( seer, TGTOPT_NOCHECK_LOS );

	if( !rune )
		return;
	endif

	if( rune.objtype != UOBJ_RUNE && rune.objtype != 0x6100 )
		SendSysMessage( seer, "You can only use this on a rune!" );
		return;
	endif

	var tox := CInt(GetObjProperty( rune, "x" ));
	var toy := CInt(GetObjProperty( rune, "y" ));
	var toz := CInt(GetObjProperty( rune, "z" ));

	if( !tox && !toy && !toz )
		SendSysMessage( seer, "That is a blank rune!" );
		return;
	endif

	set_critical( 1 );
	var gate1 := CreateItemAtLocation( seer.x, seer.y, seer.z, UOBJ_BLUE_MOONGATE2, 1 );
	var gate2 := CreateItemAtLocation( tox, toy, toz, UOBJ_BLUE_MOONGATE2, 1 );
	if (!gate1 || !gate2)
		return;
	endif

	SetObjProperty( gate1, "GateDestX", gate2.x );
	SetObjProperty( gate1, "GateDestY", gate2.y );
	SetObjProperty( gate1, "GateDestZ", gate2.z );
	SetObjProperty( gate2, "GateDestX", gate1.x );
	SetObjProperty( gate2, "GateDestY", gate1.y );
	SetObjProperty( gate2, "GateDestZ", gate1.z );

	SetObjProperty (gate1, "perma", 1);
	SetObjProperty (gate2, "perma", 1);
endprogram