use os;
use uo;
include "include/cmdlevel_check";

program storagewipe(who)
	if (!checkStaff(who, "bankwipe", CMDLEVEL_ADMINISTRATOR))
		return 0;
	endif


	var endnum, npctest, startnum := GetGlobalProperty("storagestart3");

	if (!startnum)
		startnum := 4000000;
		SetGlobalProperty("storagestart3", startnum);
	endif

	npctest := CreateNpcFromTemplate( "banker", 5445, 1153, 0 );
	endnum := npctest.serial;
	RevokePrivilege( npctest, "invul" );
	SetObjProperty(npctest, "guardkill", 1);
	ApplyDamage( npctest, 510 ); // Updated from old Damage call, ensure logic is the same.

	print("Wiping orphaned bank storage locations");

	var i;
	var acct, char, charserials := {};
	foreach acctname in ListAccounts()
		acct := FindAccount(acctname);
		for (i := 1; i <= 5; i := i + 1)
		char := acct.GetCharacter(i);
		charserials.append( char.serial );
		endfor
	endforeach

	var worldbank := FindStorageArea( "World Bank" );
	var bankboxname;
	var nuked := 0;
	for (i := startnum; i < endnum; i:= i + 1)
		bankboxname := "Bankbox of " + i;
		if ( FindRootItemInStorageArea( worldbank, bankboxname ) )

			if (i in charserials)
				//print( "Box " + i + " is safe!");
			else
				//print( "Deleting box " + i + "!");
				DestroyRootItemInStorageArea( worldbank, bankboxname );
				nuked := nuked + 1;
				SetGlobalProperty("storagestart3", i);
			endif
			sleepms(100);
		endif

	endfor

	if (i>=endnum)
		EraseGlobalProperty("storagestart3");
	endif

	print("Wiped "+nuked+" bank boxes!");

endprogram