// auction  version 1.2
// by Zulu (zulu@zuluhotel.com)   November 12, 2000

use npc;
use os;
use uo;

include "include/randname";
include "include/eventid";
include "include/client";
include "include/utility";
include "include/vetement";
include "ai/setup/modsetup";

var who, auctioneer := Self();

if (auctioneer.name["<random>"])
        SetName(auctioneer, RandomName(auctioneer) );
        Habille( auctioneer, "Clothes" );
endif

var storage:=FindStorageArea("Merchant Storage");
var inv_sell:=find_or_create_item(storage,"AUCTION",0xe7c);

if(!storage || !inv_sell) exit; endif

EnableEvents(SYSEVENT_SPEECH, 2);
EnableEvents(SYSEVENT_ITEM_GIVEN);

while (1)
    var item, event := 0;

    event := os::wait_for_event(120);

    who:=event.source;
    TurnToward(who);

    if(event.text["hello"])

	say("To learn about the auction type help or .auc help");
	say("Any items not claimed in 7 days will be deleted.");

    elseif(event.text["help"])

	say("To sell an item, simply give it to me, or say 'sell'.");
	say("We charge 10% to handle the sale.");

    elseif((event.text["holdbox"]) && (who.cmdlevel>3))

	SendOpenSpecialContainer(who,inv_sell);

    elseif(event.text["sell"])
        if(GetGlobalProperty("auctiondisabled"))
            say("Sorry, the auctions are not running at the moment.");
        elseif(GetGlobalProperty("#aucnpc")==1)
            say("I am busy right now, please try later.");
        else
        	say("Which item would you like me to auction?");
        	item := Target(who);
        	if (item)
        		SetGlobalProperty("#aucnpc",1);
       			sell_item(who,item);
        		SetGlobalProperty("#aucnpc",0);
        	else
        		say("Please do not waste my time!");
        	endif
	endif

    elseif(event.item)
        if(GetGlobalProperty("auctiondisabled"))
            say("Sorry, the auctions are not running at the moment.");
        elseif(GetGlobalProperty("#aucnpc")==1)
            say("I am busy right now, please try later.");
        else
                item:=event.item;
                if(item.objtype==0x186e && item.graphic==5360)
                        say("That item has been destroyed.");
        		DestroyItem(item);
        	else
        	SetGlobalProperty("#aucnpc",1);
       		sell_item(who,item);
        	SetGlobalProperty("#aucnpc",0);

                endif
        endif
  endif

 cleareventque();

endwhile


function cleareventque()

var ev;
repeat
until (! (ev := os::wait_for_event(0)) );

endfunction

function sell_item(who,item)
	var money, num1, bartermsg;

	if (totalauctionitems()==9)
		say("There are too many items right now.");
		return;
	endif

	num1:=CInt(RequestInput(who,who.backpack,"What is the minimum bid for " +item.desc));

	if((num1<0) || (!num1))
		SendSysMessage(who,"canceled.");
		return;
	endif

	money:=CInt(num1*0.1);

	if (money<11) money:=10; endif

	if (!who.spendgold(money))
		SendSysMessage(who,"You do not have "+money+" to cover commission.",FONT_NORMAL,34);
		return;
	endif

	bartermsg:=RequestInput(who,who.backpack,"What is your message (esc for none)");

	if(!item)
		SendSysMessage( who, "The item no longer exists.");
		return;
	endif

	if(!ReserveItem(item))
		SendSysMessage( who, "You cannot use this item.");
		return;
	endif

	if (!Accessible(who, item))
		SendSysMessage(who, "You cannot reach that.");
		ReleaseItem(item);
		return;
	endif

	if (ownitem(who.backpack,item)==0)
		SendSysMessage( who, "The item must be in your backpack.");
		ReleaseItem(item);
		return;
	endif

        if((item.objtype==0x186e && item.graphic==5360) || (item.objtype>=0xff0 && item.objtype<=0xff4))
                say("You cannot sell that.");
		ReleaseItem(item);
		return;
        endif

        if((item.objtype==0x86df) || (item.objtype==0x86df) || (item.objtype==0x14ef))
                say("You cannot sell that.");
		ReleaseItem(item);
                DestroyItem(item);
		return;
        endif

	// Placed here by Sno 6/2/03. Stops players from auctioning a deed in a bag, and
	// then placing the deed.
	if (item.isA(POLCLASS_CONTAINER))
		foreach thing in EnumerateItemsInContainer(item)
			if (!ReserveItem(thing))
				ReleaseItem(thing);
				ReleaseItem(item);
				return;
			endif
			ReleaseItem(thing);
		endforeach
	endif

	if((bartermsg=="") || (!bartermsg) || !NameValidation(bartermsg))
		bartermsg:="<none>";
	endif

        var bid:=0;
	var time:=240;
	var name  := GetObjProperty( who, "realname" );
	var tn    := GetObjProperty( who, "tn" );
	if( name && tn )
		name := name + " or " + tn;
	elseif( !name && tn )
		name := tn;
	elseif( !name )
		name := who.name;
	endif

	SetObjProperty(item,"sellaccount",who.acctname);
        SetObjProperty(item,"sellname",name);
        SetObjProperty(item,"seller",who.serial);
        SetObjProperty(item,"buyer",who.serial);
        SetObjProperty(item,"msg",bartermsg );
        SetObjProperty(item,"bid",bid);
        SetObjProperty(item,"starting",num1);
        SetObjProperty(item,"time",ReadGameClock()+time);

        MoveItemToContainer(item,inv_sell);

	tellplayers(item.desc, num1, bartermsg, name);

	ReleaseItem(item);

endfunction


function tellplayers(itemname, price, msg, unused name)
sendmsg("New item: "+ itemname+" - "+price, 300);
//sendmsg("from "+name,300,0);
if (msg!="<none>") sendmsg("msg: "+msg, 300); endif
//sendmsg("Starting bid: "+price,300,0);
endfunction


function sendmsg(msg,msgcolor)

	foreach character in EnumerateOnlineCharacters()
		if( GetObjProperty( character, "auction" ) == 1 )
			if( GetObjProperty( character, "barter" ) == 1 )
				SendSysMessage(character,msg,3,msgcolor);
			endif
		endif
	endforeach

endfunction


function totalauctionitems()

   var backpack := EnumerateItemsInContainer( inv_sell );
   var totalitems := 0;
   var timeleft;

   foreach auction_items in backpack

   timeleft := (GetObjProperty(auction_items, "time")-ReadGameClock());

   if ((!timeleft) || (timeleft==""))
   else
	totalitems := totalitems + 1;
   endif

   endforeach

   return totalitems;

endfunction


function ownitem(bpack,item)

   var backpack:=EnumerateItemsInContainer(bpack);

   foreach bpitem in backpack

	if (bpitem.serial==item.serial) return 1; endif

   endforeach

   return 0;

endfunction

function NameValidation(thestring)
  var i, invalidstr := {9,10,11,12,13};
  thestring := CAscZ(thestring);
  for(i:=1;i<=len(thestring);i:=i+1)
    if((thestring[i] in invalidstr)) return 0; endif
  endfor
  return 1;
endfunction