// Edited by Sno 17/1/03
// In the handling of SYSEVENT_DAMAGED, changed the line if(!me.criminal)  to  if(!me.criminal && !me.master.criminal).
// For some reason tamed creature's do not take the criminal status of their owner when the owner initiates an attack.

// Placed an extra check that someone hasn't illegally tamed an NPC. Calls GoWild(), sends an alert to online staff and exits.

use npc;
use os;
use uo;

include "include/eventid";
include "include/attributes";
include "include/classes";
include "include/npcbackpacks";
include "include/npccast";
include "include/randname";
include "include/string";
include "include/yesno";
include "include/rituals";
include "include/client";
include "include/spelldata";
include "ai/setup/modsetup";
include "include/random";
include "include/tamedutil";

set_critical(1);

const HALT_THRESHOLD := 10;
const MOVING_EFFECT_FIREBALL  := 0x36d4;

//only applicable for tamed (not summoned, animated and totem)
const MAX_TAMED_SERIALS:=15; //a player can only have up to 15 tamed serials at a time.
//When a tamed ai goes free they are removed. When tamed they get added.

var me := Self();
var masterserial := GetObjProperty( me, "master" );
var npccfg := ReadConfigFile( "npcdesc" );
var elem := FindConfigElem( npccfg, me.npctemplate );

if (!masterserial)
	GoWild();
endif

var master := SystemFindObjectBySerial( masterserial );
if( !master )
	master := SystemFindObjectBySerial( masterserial, SYSFIND_SEARCH_OFFLINE_MOBILES );
endif

if (!master.cmdlevel)
	if (
	!( GetObjProperty( me, "summoned" ) ||
	    GetObjProperty( me, "animated" ) ||
	    GetObjProperty( me, "charmed" ) )
	&&
	(GetObjProperty( me, "untamable" )
	|| (!GetObjProperty( me, "tamableAt" ) && !GetConfigInt( elem, "tameskill" ))))
		foreach person in EnumerateOnlineCharacters()
			if (person.cmdlevel)
				SendSysMessage(person, master.name + " attempted to tame an untamable creature!", 3, 38);
			endif
		endforeach
	 	GoWild();
	 	exit;
	endif
endif

var mypack := FindMyPack(me.serial);

var caster := 0;
var spells;
var saywords;
var firebreather;
var poisoner;
var summons := 0;
var chaseloop := 0;

var guarding :=0;
var following := 0;
var staying := 0;
var nextwatch := ReadGameClock();
var freetime := CInt(ReadGameClock() + 2400);

me.setmaster( master );

if( me.name["<random>"] )
	SetName( me, RandomName( me ) );
endif

if (GetObjProperty(me,"freetime"))
	freetime := GetObjProperty(me,"freetime");
else
	freetime := CInt(ReadGameClock() + 2400);
	SetObjProperty(me,"freetime",freetime);
endif

var dragonspeak := 0;
if ( me.npctemplate["dragon"] )
	dragonspeak := 1;
endif

set_critical(0);

program TamedAI()
	if (IsASkillTamedOrSpellboundMonster())
		if (CheckIfMasterUnderMaxTamed())
			GoWild();
			return;
		else
			AddToMastersTamedArray();
		endif
	endif 
	
	if (master.isA(POLCLASS_NPC) && !GetObjProperty(me,"Legion"))
		if (GetObjProperty(me,"#restarting"))
			GoWild();
		else
			SetObjProperty( me,"master", GetObjProperty(master,"master") );
			SetObjProperty(me,"#restarting",1);
			RestartScript(me);
		endif
	endif
	EraseObjProperty(me,"#restarting");

	if (GetObjProperty(me, "frozen"))
		me.frozen := 1;
	endif

	SetObjProperty(me,"color",me.color);

	if ( GetObjProperty( me, "regspawn" ) )
		var spawnname := GetObjProperty( me, "regspawn" );
		var numspawns := CInt(GetGlobalProperty(spawnname))-1;
		SetGlobalProperty(spawnname,numspawns);
		EraseObjProperty( me, "regspawn" );
	endif

	if (!me.backpack)
		var newbackpack := CreateItemAtLocation(5288, 1176, 0, 0xe75,1);
		EquipItem(me, newbackpack);
	endif

	SetAnchor( me.x, me.y, 20, 0 );

	if (npccfg[me.npctemplate].script == "spellkillpcs")
		caster := 1;
		spells := GetConfigStringArray( npccfg[me.npctemplate], "spell" );
		saywords := npccfg[me.npctemplate].saywords;
	elseif (npccfg[me.npctemplate].script == "firebreather")
		firebreather := 1;
	elseif (npccfg[me.npctemplate].script == "poisonkillpcs")
		poisoner := 1;
	endif

	if (!GetObjProperty(me,"#nextpoop"))
		SetObjProperty(me,"#nextpoop",ReadGameClock() + CInt(Random(1200)));
	endif

	EnableEvents( SYSEVENT_SPEECH, 12 );
	EnableEvents( SYSEVENT_ENGAGED + SYSEVENT_DAMAGED );
	EnableEvents( SYSEVENT_DOUBLECLICKED );
	EnableEvents( SYSEVENT_ITEM_GIVEN );
	EnableEvents( EVID_PEACEMADE );

	SetWarMode( 0 );
	SetObjProperty(me, "serial", me.serial);

	MainAILoop();

endprogram

function Fight( opponent )

	var targetname := opponent.name;
	var oldfollowing := following;

	SetDexterityMod(me, 0);
	following := 0;

	if ( (opponent.serial == me.serial) || (opponent.serial == master.serial) )
		return;
	endif

	TurnToward( opponent );
	SetOpponent( opponent );

	var nextbreath := ReadGameClock();
	var ev;

	var loops := 0;
	var waittime := 0;

	while ( (opponent) &&
	        not (opponent.dead || opponent.hidden || opponent.concealed) &&
	        (Distance(me,opponent) < 20) )


		if (!CloseDistance( opponent ) )
			loops := loops + 1;
			waittime := 0;
		else
			loops := 0;
			waittime := 1;
		endif

		ev := wait_for_event( waittime );

		case (ev.type)
			EVID_ALL_ATTACK_CMD:
				opponent := ev.target;
				SetOpponent( ev.target );
			EVID_ALL_FOLLOW_CMD:
				following := ev.target;
			EVID_ALL_GUARD_CMD:
				guarding := ev.target;
				PrintTextAbove(me,"*guarding " + guarding.name + "*");
			SYSEVENT_SPEECH:
				ProcessSpeech(ev);
			SYSEVENT_ENGAGED:
				if (ev.source)
					if ( (ev.source.serial != opponent.serial) && (ev.source.serial != master.serial) &&
					     (ev.source.serial != me.serial) )
						if( (!CheckLineOfSight95( me, opponent )) || (Random(8)==1) )
							opponent := ev.source;
							SetOpponent( opponent );
						endif
					endif
					TurnToward( opponent );
				endif
			SYSEVENT_DAMAGED:
				criminalCheck(ev.source); // Edited by Sno, 11/2/03
				if (ev.source)
					if ( (ev.source.serial != opponent.serial) && (ev.source.serial != master.serial) &&
					     (ev.source.serial != me.serial) )
						if( (!CheckLineOfSight95( me, opponent )) || (Random(8)==1) )
							opponent := ev.source;
							SetOpponent( opponent );
						endif
					endif
					TurnToward( opponent );
				endif				
			EVID_PEACEMADE:
				SetWarMode( 0 );
				opponent := 0;
				following := oldfollowing;
				return;
		endcase

		if (following)
			SetWarMode( 0 );
			opponent := 0;
			staying := 0;
			return;
		endif

		if (firebreather)
			if ( nextbreath <= ReadGameClock() )
				cast_meteor();
				nextbreath := ReadGameClock()+Random(5)+5;
			endif
		elseif (caster)
			if ( nextbreath <= ReadGameClock() )
				sleep(1);
				cast_offensive_spell(me, opponent);
				nextbreath := ReadGameClock()+Random(10)+10;
			endif
		elseif (poisoner)
			if ( nextbreath <= ReadGameClock() )
				DoPoison(opponent);
				nextbreath := ReadGameClock()+Random(10)+10;
			endif
		endif

		if ( loops > 100 )
			flee(opponent);
			return;
		endif

	endwhile

	SetWarMode( 0 );
	opponent := 0;

	foreach mob in ListHostiles( me, 9, 0 )
		Fight(mob);
		return;
	endforeach

	if ( Distance(me, master) < 8 )
		following := oldfollowing;
	else
		following := 0;
		staying := 0;
	endif

	if (dragonspeak) dragontalk("aftercombat", targetname); endif

endfunction

function CloseDistance( opponent )
	var sleepdelay := 300 - (CInt(GetDexterity(me)) * 1.5);
	if (sleepdelay < 0) sleepdelay := 0; endif

	if( Distance( me, opponent ) <= 1)
		return 1;
	else
		if(!RunToward( opponent ))
			sleepms(sleepdelay);
			return 1;
		else
			return 0;
		endif
	endif

endfunction


function Transfer()

	if( GetObjProperty( me, "summoned" ) ||
	    GetObjProperty( me, "animated" ) ||
	    GetObjProperty( me, "charmed" ) || 
	    GetObjProperty( me, "specialsteed" ))
		return;
	endif

	SendSysMessage( master, "Transfer " + me.name + " to whom?" );
	var whom := Target( master, TGTOPT_NOCHECK_LOS );
	if(!whom)
		SendSysMessage( master, "Canceled" );
		return;
	endif

	if (!YesNo(whom,"Accept " + master.name + " pet?"))
		SendSysMessage( master, whom.name + " has not accepted the transfer." );
		return;
	endif

        if (GetObjProperty(me, "PersonalizedMount"))
          if (GetObjProperty(me, "PersonalizedMount") == 1)
            me.color := 0;
          else
            me.color := GetObjProperty(me, "PersonalizedMount");
          endif
          EraseObjProperty(me, "PersonalizedMount");
        endif

	if ( (firebreather) || (caster) )

		var tameskill := npccfg[me.npctemplate].tameskill;
		var tgttaming := GetEffectiveSkill(whom, SKILLID_TAMING)+20;
		if ( tgttaming < tameskill )
			SendSysMessage( master, "That has no chance of controlling this beast!" );
			return;
		endif

	endif

	RemoveFromMastersTamedArray();	// Added by seravy so tamed_serials is updated.

	SetObjProperty( me, "master", whom.serial );
	PrintTextAbove(me, CStr(me.name + " accepts " + whom.name+ " as its new master"));
	RestartScript( me );

endfunction


function Release()

	var summoned := GetObjProperty( me, "summoned" );
	var animated := GetObjProperty( me, "animated" );
	var totem := CInt(GetObjProperty( me, "totem" ));
	var specialsteed := CInt(GetObjProperty( me, "specialsteed" ));
	if( totem )
		set_critical(1);
		var it := CreateItemAtLocation(me.x,me.y,me.z,totem,1);
		if (it)
			it.decayAt := 0;
			it.name := me.name;
			it.color := CInt(GetObjProperty( me, "totemcolor" ));
			SetObjProperty(it,"critter",me.npctemplate);
			SetObjProperty(it,"critcolor",me.color);
			SetObjProperty(it,"totemhp",GetHp(me));
			it.usescript := ":dundee:totem";
			it.decay := 0;
			it.movable := 1;
			PlaySoundEffect( me, SFX_SPELL_DISPEL );
			PlayStationaryEffect( me.x, me.y, me.z, FX_SMOKE, 0xa, 0xa );
			MoveObjectToLocation(me,5288, 1176, 0, _DEFAULT_REALM,MOVEOBJECT_FORCELOCATION); //AutoConversion
			RevokePrivilege( me, "invul" );
			ApplyDamage( me, GetMaxHp(me) + 3 );
			set_critical(0);
		else
			say("*Argh*");
			return;
		endif
	elseif( summoned )
		PlayStationaryEffect( me.x, me.y, me.z, FX_SMOKE, 0xa, 0xa );
		MoveObjectToLocation(me,5288, 1176, 0, _DEFAULT_REALM,MOVEOBJECT_FORCELOCATION); //AutoConversion
		RevokePrivilege( me, "invul" );
		ApplyRawDamage( me, GetHp(me) + 3 );
	elseif( animated || specialsteed)
		RevokePrivilege( me, "invul" );
		ApplyRawDamage( me, GetHp(me) + 3 );
	else
		GoWild();
	endif

endfunction

function Fetch()

	if ( len(EnumerateItemsInContainer(mypack)) )
		drop();
	endif

	var tobj := Target(master);
	if (tobj.container)
		return;
	endif

	var times := 0;
	while ( (Distance(me, tobj) > 1) && (times < 5) && CheckLineOfSight95(me,tobj) )
		if( !RunToward(tobj))
			sleep(1);
			times := times + 1;
		else
			times := 0;
		endif
	endwhile

	if (Accessible(me,tobj) && tobj.movable)
		var result := MoveItemToContainer(tobj, mypack);
	endif

endfunction

function drop()
	foreach myitems in EnumerateItemsInContainer(mypack)
		if (myitems.container.serial == mypack.serial)
			MoveObjectToLocation(myitems, me.x, me.y, me.z, _DEFAULT_REALM, 0); //AutoConversion
			sleepms(100);
		endif
	endforeach
endfunction

function speak()

	PetCount();

	if (Random(4)==1)
		var sound_number := npccfg[me.npctemplate].attackhitsound;
		if (sound_number)
			PlaySoundEffect( me, sound_number );
		endif
	endif

endfunction

function PetCount()

	var npctemplate := me.npctemplate;

	var imfrommagic;
	var imanelelord;

	if( GetObjProperty( me, "summoned" ) ||
	    GetObjProperty( me, "animated" ) ||
	    GetObjProperty( me, "charmed"  ) )
		imfrommagic := 1;
	endif

	if( npctemplate["elementallord"] )
		imanelelord := 1;
	endif

	var intel	 := ModifyWithMagicEfficiency( master, GetIntelligence(master) );
	var pets	 := 0;
	var magic_pets := 0;
	var mobs := ListMobilesNearLocation( me.x, me.y, me.z, 8 );
	foreach mob in mobs
		var mob_template := mob.npctemplate;
		if( (mob.script == "tamed") && ( GetObjProperty( mob, "master" ) == masterserial ) )
			if( GetObjProperty( mob, "summoned" ) || GetObjProperty( mob, "animated" ) || GetObjProperty( mob, "charmed" ) )
				if( mob_template["elementallord"] )
					if( imanelelord && mob.serial != me.serial )
						SpecialFrenzyRelease( mob );
					else
						magic_pets := magic_pets + 3;
					endif

				else
					var val := 1;
					if( (GetIntelligence(mob) > intel) || (GetMaxHp(mob) > intel) )
						val := 2;
					endif

					if( GetObjProperty( mob, "animated" ) || GetObjProperty( mob, "charmed" ) )
						val := val * 2;
					endif

					magic_pets := magic_pets + val;
				endif

			elseif( npccfg[mob_template].script == "spellkillpcs" )
				if( GetMaxHp(mob) > 500)
					pets := pets + 9;
				elseif( GetMaxHp(mob) > 250)
					pets := pets + 6;
				else
					pets := pets + 3;
				endif

			elseif( npccfg[mob_template].script == "firebreather" )
				if( GetMaxHp(mob) > 500)
					pets := pets + 12;
				elseif( GetMaxHp(mob) > 250 )
					pets := pets + 8;
				else
					pets := pets + 4;
				endif

			else
				if( GetMaxHp(mob) > 300)
					pets := pets + 3;
				elseif (GetMaxHp(mob) > 150)
					pets := pets + 2;
				else
					pets := pets + 1;
				endif
			endif
		endif
	endforeach

	if( imfrommagic )
		var magery	:= ModifyWithMagicEfficiency( master, GetEffectiveSkill( master, SKILLID_MAGERY ) );
		var maxVar	:= CInt( magery / 20 );
		if( magic_pets > maxVar && !master.cmdlevel && magic_pets > 2 )
			SpecialFrenzyRelease( master );
		endif

	else
		var lore 	:= CInt( GetEffectiveSkill( master, SKILLID_ANIMALLORE ) * ClasseBonus( master, CLASSEID_RANGER ));
		var taming	:= CInt( GetEffectiveSkill( master, SKILLID_TAMING ) * ClasseBonus( master, CLASSEID_RANGER ));
		var maxVar	:= CInt( (lore + taming) / 15 );
		if( pets > maxVar && !master.cmdlevel && pets > 2 )
			Release();
		endif
	endif

endfunction


function SpecialFrenzyRelease( against )

	if (IsASkillTamedOrSpellboundMonster())
		RemoveFromMastersTamedArray();
	endif

	me.script	:= npccfg[me.npctemplate].script;
	me.name		:= npccfg[me.npctemplate].name;
	if( me.name["<random>"] )
		SetName( me, RandomName( me ) );
	endif

	PrintTextAbove( me, me.name + " can roam free again!" );
	me.setmaster( 0 );
	EraseObjProperty( me, "master" );
	SetObjProperty( me, "tametimer", ReadGameClock() + 10000 );
	RestartScript( me );

	var ev := array;
	ev.+ type;
	ev.+ source;
	ev.type := EVID_PEACEMADE;
	ev.source := against;
	SendEvent( me, ev );

	ev.type := SYSEVENT_ENGAGED;
	SendEvent( me, ev );

endfunction


function OpenMyPack();
	
	
	if (!master)
		master := SystemFindObjectBySerial( masterserial );
		me.setmaster( master );
	endif

	
	
	if (Distance(me,master) > 1)
		return;
	endif

	case (me.graphic)
		0x123:
		0x124:
			if (!me.backpack)
				var newbackpack := CreateItemAtLocation(5288, 1176, 0, 0xe75,1);
				EquipItem(me, newbackpack);
			endif

			foreach item in EnumerateItemsInContainer(mypack)
				if (item.container.serial == mypack.serial)
					MoveItemToContainer(item, me.backpack);
				endif
			endforeach

			SendOpenSpecialContainer( master, me.backpack );
			return;
	endcase

	if( GetObjProperty( me, "summoned" ) || GetObjProperty( me, "animated" ) || GetObjProperty( me, "charmed" ) )
		SendSysMessage( master, "You can't mount summoned or magically binded creatures." );
		return;
	endif
	if (GetObjProperty(me, "specialsteed"))
		if (me.npctemplate=="deathknightsteed" && GetClass(master)!=CLASSEID_DEATHKNIGHT)
			SendSysMessage(master, "The animal refuses to accept you as a rider");
		elseif (me.npctemplate=="paladinsteed" && GetClass(master)!=CLASSEID_PALADIN)
			SendSysMessage(master, "The animal refuses to accept you as a rider");
		endif
	endif			
	master.hidden := 0;

	var alreadymounted := GetEquipmentByLayer( master, 25 );
	if (alreadymounted)
		return;
	endif
	
	if (master.dead)
	
		return;
	endif
	
	if (master.z != me.z)
		return;
	endif


	var mounttype := 0;

    case(me.graphic)
      0xcc: mounttype := 0x3ea2;  // Horse
      0xc8: mounttype := 0x3e9f;  // Horse
      0xe2: mounttype := 0x3ea0;  // Horse
      0xe4: mounttype := 0x3ea1;  // Horse
      0xdc: mounttype := 0x3ea6;  // Llama
      0xd2: mounttype := 0x3ea3;  // Desert Ostard
      0xda: mounttype := 0x3ea4;  // Fire etc Ostard
      0xdb: mounttype := 0x3ea5;  // Ice etc Ostard
      177:  mounttype := 0x3ea7;  // Nightmare
      117:  mounttype := 0x3ea8;  // Silver Steed
      114:  mounttype := 0x3ea9;  // Nightmare
      115:  mounttype := 0x3eaa;  // Ethereal Horse
      170:  mounttype := 0x3eab;  // Ethereal Llama
      170:  mounttype := 0x3eac;  // Ethereal Ostard
     0xC3:  mounttype := 0x3e97;  // Ethereal Giant Beetle
     0xC2:  mounttype := 0x3e98;  // Ethereal Swamp Dragon
     0xC1:  mounttype := 0x3e9a;  // Ethereal Ridgeback
	  0xBE:  mounttype := 0x3e9e;  // Firesteed
     0xBF:  mounttype := 0x3e9c;  // Ethereal Kirin
     0xC0:  mounttype := 0x3e9d;  // Ethereal Unicorn
     0xC8:  mounttype := 0x3e9f;  // Light Brown horse
      120:  mounttype := 0x3eaf;  // War Horse (Blood Red)
      121:  mounttype := 0x3eb0;  // War Horse (Light Green)
      119:  mounttype := 0x3eb1;  // War Horse (Light Blue)
      144:  mounttype := 0x3eb3;  // Sea Horse (Medium Blue)
      116:  mounttype := 0x3eb5;  // Nightmare
      178:  mounttype := 0x3eb6;  // Nightmare 4
      132:  mounttype := 0x3ead;  // Kirin
      118:  mounttype := 0x3eb2;  // War Horse (Purple)
      122:  mounttype := 0x3eb4;  // Unicorn
      116:  mounttype := 0x3eb5;  // Nightmare
      179:  mounttype := 0x3eb7;  // Dark Steed
      187:  mounttype := 0x3eb8;  // Ridgeback
      188:  mounttype := 0x3eba;  // Ridgeback, Savage
      793:  mounttype := 0x3ebb;  // Skeletal Mount
      791:  mounttype := 0x3ebc;  // Beetle
      794:  mounttype := 0x3ebd;  // SwampDragon
      799:  mounttype := 0x3ebe;  // Armored Swamp Dragon
    endcase

	if (mounttype)
		if (!(master.graphic == 400 ||master.graphic == 401) && !master.cmdlevel)
			SendSysMessage(master, "You cannot use this mount in this form");
			return;
		endif
		var staff	:= GetEquipmentByLayer( master, LAYER_HAND2 );
		var robe	:= GetEquipmentByLayer( master, LAYER_ROBE );
		if (GetObjProperty(me, "nomounting"))
			SendSysMessage( master, "This creature is not suitable for riding");
		elseif( staff.objtype == OAK_STAFF || robe.objtype == RITUAL_ROBE )
			SendSysMessage( master, "You can't ride anything while wearing ritual clothing." );
		elseif ( MoveObjectToLocation(master,me.x,me.y,me.z, _DEFAULT_REALM, 0) )

			set_critical(1);
			master.facing := me.facing;
			var mount := CreateItemAtLocation(5288, 1176, 0, 0xf021,1);
			mount.color := me.color;
			if (GetObjProperty(me, "specialsteed"))
				SetObjProperty(master, "currsteed", mount.serial);
				SetObjProperty(mount, "specialsteed", 1);
			endif
			SetObjProperty(mount,"petname",me.name);
			SetObjProperty(mount,"pethp",GetHp(me));
			SetObjProperty(mount,"petmana",GetMana(me));
			SetObjProperty(mount,"freetime",freetime);
			SetObjProperty(mount,"npctemplate",me.npctemplate);
			if (GetObjProperty(me, "PersonalizedMount"))
				SetObjProperty(mount,"PersonalizedMount", GetObjProperty(me, "PersonalizedMount"));
			endif
			MoveObjectToLocation(me, 5288, 1176, 0, _DEFAULT_REALM, MOVEOBJECT_FORCELOCATION); //AutoConversion
			mount.graphic := mounttype;
			EquipItem(master,mount);
			RevokePrivilege( me, "invul" );
			SetObjProperty(me,"guardkill",1);
			ApplyDamage( me, GetMaxHp(me) + 3 );

			set_critical(0);
			sleep(1);

			MoveObjectToLocation(me, 5288, 1176, 0, _DEFAULT_REALM, MOVEOBJECT_FORCELOCATION); //AutoConversion
			RevokePrivilege( me, "invul" );
			SetObjProperty(me,"guardkill",1);
			ApplyDamage( me, GetMaxHp(me) + 3 );

		endif
	endif

endfunction

function poop();

	if ( ReadGameClock() > CInt(GetObjProperty(me,"#nextpoop")) )
		if (Random(4)==1)

			var poopoo := 0;
			case (me.graphic)
				0xcc:
				0xe4:
				0xc8:
				0xe2:
				0x123:	poopoo := CreateItemAtLocation(me.x,me.y,me.z,0x0f3c,1);
				default: return;

			endcase

			if (poopoo)
				poopoo.decayAt := ReadGameClock() + 300;
				poopoo.movable := 0;
			endif

		endif
	endif
	SetObjProperty(me,"#nextpoop",ReadGameClock() + 1800);

endfunction

function TakeItem(ev)
	var foodtype := npccfg[me.npctemplate].food;
	if (!foodtype)
		foodtype := "all";
	endif

	var cfg := ReadConfigFile("::food");
	var elem := FindConfigElem(cfg,foodtype);
	var foodarray := GetConfigStringArray(elem,"food");
	if (CStr(ev.item.objtype) in foodarray )
		PlaySoundEffect(me,CInt(0x3b) + Random(3));
		DestroyItem(ev.item);
		PrintTextAbovePrivate(me,"*Yum!*",ev.source);
		freetime := ReadGameClock() + 14400;
		SetObjProperty(me,"freetime",freetime);
		return;
	endif

	if ( (me.graphic == 0x123 ) || (me.graphic == 0x124) )
		if (!me.backpack)
			var newbackpack := CreateItemAtLocation(5288, 1176, 0, 0xe75,1);
			EquipItem(me, newbackpack);
		endif
		MoveItemToContainer(ev.item,me.backpack);
		PlaySoundEffect(me,0x49);
	else
		var yourpack := ev.source.backpack;
		MoveItemToContainer(ev.item,yourpack);
	endif

endfunction

function MainAILoop()

	var ev;
	var evtext;
	var mename := lower(me.name);
	var warnings := 0;

	nextwatch := ReadGameClock();
	var waittime := 120;

	if (!GoToSleep() )
		waittime := 2;
	endif

	var loops := 60;
	var firstwander := 1;

	while (1)
		ev := os::wait_for_event( waittime );
		if ( ev )
			if (!ev.source.npctemplate)
				waittime := 2;
				loops := 0;
			endif
			case (ev.type)
				SYSEVENT_SPEECH:
					ProcessSpeech(ev);
				SYSEVENT_ENGAGED:
				SYSEVENT_DAMAGED:
					stopme();
					if (ev.source)
						if (dragonspeak) dragontalk("attacked", ev.source.name); endif
						Fight( ev.source );
					endif
				SYSEVENT_DOUBLECLICKED:
					if (ev.source.serial == masterserial)
						OpenMyPack();
					endif
				SYSEVENT_ITEM_GIVEN:
					if (ev.source.serial == masterserial)
						TakeItem(ev);
					endif
				EVID_ALL_ATTACK_CMD:
					stopme();
					Fight(ev.target);
				EVID_ALL_FOLLOW_CMD:
					stopme();
					following := ev.target;
				EVID_ALL_GUARD_CMD:
					stopme();
					guarding := ev.target;
					PrintTextAbove(me,"*guarding " + guarding.name + "*");
			endcase
		endif

		if ( (following) && (master) )
			waittime := 0;
			firstwander := 1;
			staying := 0;
			loops := 0;
			SetAnchor( me.x, me.y, 20, 0 );
			Follow();
		elseif ( (guarding) && (master) )
			waittime := 7;
			firstwander := 1;
			loops := 0;
			SetAnchor( me.x, me.y, 20, 0 );
			Guard();
		else
			following := 0;
			guarding := 0;
			if ( firstwander )
				SetAnchor( me.x, me.y, 2, 80 );
				waittime := 2;
			endif
			firstwander := 0;
			if ((master) && (!staying))
				wander();
			endif
			EraseObjProperty(me,"#flees");
			loops := loops + 1;
		endif

		if (loops > 60)
			if ( GoToSleep() )
				waittime := 120;
			else
				waittime := 2;
				loops := 0;
			endif
		endif

		if (ReadGameClock() > freetime )
			if ( GetObjProperty( me, "totem" ) )
				freetime := CInt(ReadGameClock()+1200);
				SetObjProperty(me,"freetime",freetime);
			elseif (!GetObjProperty( me, "summoned") )
				warnings := warnings + 1;
				if ( warnings > 4 )
					Release();
					freetime := CInt(ReadGameClock()+1200);
					SetObjProperty(me,"freetime",freetime);
				else
					say("*Looks Hungry*");
					speak();
					freetime := CInt(ReadGameClock()+1200);
					SetObjProperty(me,"freetime",freetime);
				endif
			else
				Release();
			endif
		endif

	// Poop();

	endwhile

endfunction

function Guard()

	if ( nextwatch <= ReadGameClock() )
		nextwatch := ReadGameClock() + 7;
		if ( Random(20)==1 )
			PrintTextAbove(me,"*guarding " + guarding.name + "*");
		endif
		foreach mob in ListHostiles( guarding, 9, 0 );
			if (dragonspeak) dragontalk("guardingattacked", mob.name); endif
			Fight(mob);
			return;
		endforeach
	endif

	if ( (!GetStrength(guarding)) || (guarding.dead) || (Distance(me,guarding) >
15) )
		guarding := 0;
	endif

endfunction

function Follow()

//	var strongwill:=0;

   //if ( me.npctemplate["dragon1"] || me.npctemplate["dragon2"]||me.npctemplate["nightmare"])//too strong to exploit with
	//    strongwill := 1;
 //  endif

   //if(following.warmode && !strongwill)
       //	SendSysMessage(following, "The creature is hesitant to follow you");//doesn't want to follow an angry master
       // 	RestartScript(me);//he is constantly being flooded with follows, so most effective way is to restartscript
       //	return;
	//endif

	if ( (me.frozen) )
		sleep(10);
		return;
	endif

	SetDexterityMod(me, 5000);
	var d := Distance( me, following );
	if (d < 2)
		sleep(1);
		chaseloop := 0;
	elseif (( d > 10 ) || ( chaseloop > 50 ))
		MoveObjectToLocation( me, following.x, following.y, following.z, _DEFAULT_REALM, MOVEOBJECT_FORCELOCATION ); //AutoConversion
		chaseloop := 0;
		sleep(1);
	else
		RunToward(following);
		chaseloop := chaseloop + 1;
	endif

	if ( (!GetStrength(following)) || (following.dead) )
		following := 0;
	endif
	SetDexterityMod(me, 0);
	if (guarding)
		Guard();
	endif

endfunction

function ProcessSpeech(ev)

	if( ev.source.serial != masterserial )
		return;
	endif

	if (!master)
		RestartScript( me );
	endif

	speak();

	var evtext := lower(ev.text);
	var mename := lower(me.name);

	if (evtext[mename + " kill"] || evtext[mename + " attack"] ||
	    evtext["all kill"] || evtext["all attack"] )
		stopme();
		var what := Target( master, TGTOPT_HARMFUL + TGTOPT_CHECK_LOS );
				if (what.hidden == 1)
					return;
				endif
				if (what.multi && (me.multi.serial != what.multi.serial))
					return;
				endif
		if (what)
			if (master.hidden)
				master.hidden := 0;
			endif
			if (Distance(me, what) > 15)
				return;
			endif

			if(IsInGuardedArea(what))
				start_script( ":areas:callguards", me);
			endif

			if (dragonspeak) dragontalk("attack", what.name); endif
			if (evtext["all kill"] || evtext["all attack"] )
				AllCommand(EVID_ALL_ATTACK_CMD, what);
			else
				Fight( what );
			endif

		endif


	elseif( evtext[mename + " stop"] || evtext["all stop"])
		stopme();
	elseif ((evtext[mename + " come"]) || (evtext["all come"]))
		var done := 0;
		chaseloop := 0;
		while( done == 0 )
			chaseloop := chaseloop +1;
			done := CloseDistance( ev.source );
			if (chaseloop > 25)
				done :=1;
			endif
		endwhile
	elseif (evtext[mename + " follow"])
		stopme();
		if (evtext["follow me"])
			following := ev.source;
		else
			var what := Target( master, TGTOPT_CHECK_LOS );
			if (what)
				if (!CheckLineOfSight95(me, what)) //barns - to stop exploit
					return;
				endif
				if (what.multi && (me.multi.serial != what.multi.serial))
					return;
				endif
				if (what.hidden == 1)
					return;
				endif
				following := what;
			endif
		endif
		if (dragonspeak) dragontalk("follow", following.name); endif
	elseif (evtext["all follow"])
		if (evtext["follow me"])
			following  := ev.source;
		else
			stopme();
			var what := Target( master, TGTOPT_CHECK_LOS );
			if (what)
				if (!CheckLineOfSight95(me, what)) //barns - to stop exploit
					return;
				endif
				if (what.multi && (me.multi.serial != what.multi.serial))
					return;
				endif
				if (what.hidden == 1)
					return;
				endif
				AllCommand(EVID_ALL_FOLLOW_CMD, what);
			endif
		endif
		if (dragonspeak) dragontalk("follow", following.name); endif
	elseif (evtext[mename + " transfer"] || (evtext["all transfer"]))
		stopme();
		Transfer();
	elseif (evtext[mename + " release"] || (evtext["all release"]))
		Release();
	elseif (evtext[mename + " guard"])
		if (evtext["guard me"])
			guarding := ev.source;
			PrintTextAbove(me,"*guarding " + guarding.name + "*");
		else
			stopme();
			var what := Target( master, TGTOPT_HELPFUL + TGTOPT_CHECK_LOS );
			if (GetHp(what))
				if (!CheckLineOfSight95(me, what)) //barns - to stop exploit
					return;
				endif
				if (what.multi && (me.multi.serial != what.multi.serial))
					return;
				endif
				if (what.hidden == 1)
					return;
				endif
				guarding := what;
				PrintTextAbove(me,"*guarding " + guarding.name + "*");
			endif
		endif
	elseif (evtext["all guard"])
		if (evtext["guard me"])
			guarding := ev.source;
			PrintTextAbove(me,"*guarding " + guarding.name + "*");
		else
			stopme();
			var what := Target( master, TGTOPT_HELPFUL + TGTOPT_CHECK_LOS );
			if (GetHp(what))
				if (!CheckLineOfSight95(me, what)) //barns - to stop exploit
					return;
				endif
				if (what.multi && (me.multi.serial != what.multi.serial))
					return;
				endif
				if (what.hidden == 1)
					return;
				endif
				AllCommand(EVID_ALL_GUARD_CMD, what);
			endif
		endif
	elseif (evtext[mename + " fetch"]  || (evtext[mename + " get"]))
		stopme();
		if (dragonspeak) dragontalk("fetch", master.name); endif
		Fetch();
	elseif (evtext[mename + " drop"])
		//stopme();
		if (dragonspeak) dragontalk("drop", master.name); endif
		drop();
	elseif (evtext[mename + " speak"]  || (evtext["all speak"]))
		speak();
	elseif (evtext[mename + " stay"]  || (evtext["all stay"]))
		staying := 1;
	endif

endfunction

function AllCommand(evtype,what)

	var mobs := ListMobilesNearLocation( master.x, master.y, master.z, 9 );
	var eve array;
	eve.+type := evtype;
	eve.+source := me;
	eve.+target := what;
	SendEvent( me, eve );
	foreach mob in mobs
		if( GetObjProperty( mob, "master" ) == me.master.serial )
			SendEvent( mob, eve );
		endif
	endforeach

endfunction

function cast_meteor()

	var targets := ListMobilesInLineOfSight(me, 3);

	foreach mobile in targets
		var dmg := CInt( GetHp(me) / 15 ) + RandomDiceStr("1d15");
		PlayMovingEffect(me, mobile,MOVING_EFFECT_FIREBALL, 10,1,1);
		ApplyElementalDamageNoResist( mobile, me, dmg, FIRE );
	endforeach

endfunction

function DoPoison( you )

	if ( (Distance(me,you)<2) && (Random(8)==1) )

		var p_level := Random(CInt(GetMaxHp(me)/20));
		if (p_level > 3) p_level := 3; endif
		SetPoison(you, p_level, "You have been poisoned by " + me.name +"!");

	endif

endfunction


function flee(opponent)

	var numflees := GetObjProperty(me,"#flees");

	if (numflees)
		if ( numflees > 5 )
			EraseObjProperty(me,"#flees");
			var totem := CInt(GetObjProperty( me, "totem" ));
			if (totem)
				sleep(30);
				return;
			else
				MoveObjectToLocation(me,5288,1176,0, _DEFAULT_REALM,MOVEOBJECT_FORCELOCATION); //AutoConversion
				RevokePrivilege( me, "invul" );
				SetObjProperty(me,"guardkill",1);
				ApplyDamage( me, GetMaxHp(me) + 3 );
			endif
		else
			numflees := numflees + 1;
		endif
	else
		numflees := 1;
	endif

	SetObjProperty(me,"#flees",numflees);

	var runs := 0, sleepdelay;
	while ( (Distance(me, opponent) < 20) && (runs < 50) )
	  sleepdelay := 300 - (CInt(GetDexterity(me)) * 1.5);
	  if (sleepdelay < 0) sleepdelay := 0; endif

		RunAwayFrom(opponent);
		sleepms(sleepdelay);
		runs := runs +1;
		wait_for_event(0);
	endwhile

	RestartScript( me );

endfunction

function GoWild()

        if (GetObjProperty(me, "PersonalizedMount"))
          if (GetObjProperty(me, "PersonalizedMount") == 1)
            me.color := 0;
          else
            me.color := GetObjProperty(me, "PersonalizedMount");
          endif
          EraseObjProperty(me, "PersonalizedMount");
        endif

	if (IsASkillTamedOrSpellboundMonster())
		RemoveFromMastersTamedArray();
	endif
	
	
	me.script := npccfg[me.npctemplate].script;
	me.name := npccfg[me.npctemplate].name;
	if( me.name["<random>"] )
		SetName( me, RandomName( me ) );
	endif
	PrintTextAbove( me, me.name + " can roam free again!" );
	me.setmaster( 0 );
	EraseObjProperty(me, "master");
	SetAnchor(me.x,me.y,10,1 );

	if(IsInGuardedArea(me))
		start_script( ":areas:callguards", me );
	endif

	RestartScript(me);

endfunction

function GoToSleep()

	foreach critter in ListMobilesNearLocation( me.x,me.y,me.z, 10 );
		if (!critter.isA(POLCLASS_NPC))
			return 0;
		endif
	endforeach

	return 1;

endfunction

function dragontalk(responsekey, targetname)

	if (Random(3)!=1) return; endif

	targetname := truncatearticle(targetname);
	var speachfile := ReadConfigFile( ":dundee:dragonspeak" );
	if (!speachfile) return; endif
	var elem := FindConfigElem( speachfile, responsekey );
	if (!elem) return; endif
	var responses := GetConfigStringArray(elem,"response");
	if (!responses) return; endif
	var response := responses[ Random(len(responses))+1 ];
	response["mastername"] := master.name;
	response["targetname"] := targetname;

	say(response);

endfunction

// attacker is someone who has damaged it (not necessarily started the fight)
function criminalCheck(attacker)
	if( !master.cmdlevel )
		var guild1 := GetObjProperty( master, "guild_id" );
		if( guild1 )
			var guild2 := GetObjProperty( attacker, "guild_id" );
			if( guild2 )
				if( guild1 == guild2 )
					return;
				else
					guild1 := FindGuild( guild1 );
					guild2 := FindGuild( guild2 );
					if( guild1.IsEnemyGuild(guild2) )
						return;
					elseif( guild1.IsAllyGuild(guild2) )
						return;
					endif
				endif
			endif
		endif
	
		if( !master.criminal )
			// Set criminal only if the the master is not a criminal
			attacker.SetCriminal(1);
		endif
	endif
endfunction

function stopme()
	guarding := 0;
	following := 0;
endfunction

function AttemptPossession(who, me)
	SendSysMessage(who, "You attempt to possess " + who.me);
	
endfunction