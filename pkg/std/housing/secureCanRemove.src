use uo;

include "utility";

program CanRemoveScript(who, container, unused item, movetype)
	if ( movetype != MOVETYPE_PLAYER )
		return 1;
	endif

	// Treasure chest. ----------------------------------------------------------------------

	if ( GetObjProperty(container, "treasurechest") )
		if (who.cmdlevel)
			return 1;
		endif
		if (CInt(GetObjProperty(who, "#looting")) > ReadGameClock()) // is already looting
			SendSysMessage(who, "You must wait before looting again.");
			return 0;
		else
			SetObjProperty(who, "#looting", ReadGameClock() + 1); // else set a timeout (just in case the script doesn't end)
			return 1;
		endif
	endif

	// House secure. ------------------------------------------------------------------------

	var top := getTopContainer(container);
	var houseSerial := GetObjProperty(top, "houseserial");
	if ( houseSerial )
		var house := SystemFindObjectBySerial(houseSerial);
		if (house)
			if (IsOwner(who, house) || IsFriend(who, house))
				return 1;
			else
				return 0;
			endif
		endif
	endif

	return 1;
endprogram

function getTopContainer(container)
	while (container.container)
		container := container.container;
	endwhile

	return container;
endfunction