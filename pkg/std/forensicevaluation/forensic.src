use uo;
use os;


include "include/client";
include "include/objtype";

const POINTS := 200;

program forensic( who )

	SendSysMessage( who, "What would you like to evaluate?" );
	var what := Target( who );
	if( !what )
		return;
	endif

	if( Distance( who, what ) > 1 )
		SendSysMessage( who, "You're too far away." );
		return;
	endif

	if( what.objtype != UOBJ_CORPSE )
		if (what.isa(POLCLASS_WEAPON))
			EvaluateWeapon(who, what);
			return;
		else
			SendSysMessage( who, "You can't find anything useful on that." );
		endif
		return;
	endif

	FaceIt( who, what );

	PrintTextAbove( who, "Let me see..." );
	PerformAction( who, ANIM_BOW );
	sleepms( 1500 );
	PrintTextAbove( who, "Hmmmm..." );
	sleepms( 1000 );

	if( CheckSkill( who, SKILLID_FORENSICS, -1, POINTS ) )
		var killer  := GetObjProperty( what, "KilledBy" );
		var chopper := GetObjProperty( what, "ChoppedBy" );
		var looters := GetObjProperty( what, "LootedBy" );
		if( !looters )
			looters := dictionary;
		endif

		if( killer || chopper || (looters.keys()).size() )
			PrintTextAbove( who, "I think I got it..." );

			if( killer )
				PrintTextAbove( who, "I think it was killed by " + killer + "." );
				sleep( 2 );
			endif

			if( (looters.keys()).size() )
				PrintTextAbove( who, "I see that it was looted by: " );
				foreach serial in (looters.keys())
					PrintTextAbove( who, looters[serial] );
					sleep( 1 );
				endforeach
			endif

			if( chopper )
				PrintTextAbove( who, "Ohh..." );
				sleep( 1 );
				PrintTextAbove( who, "It was brutally chopped by " + chopper + "." );
			endif
		else
			PrintTextAbove( who, "I have no clues..." );
		endif
	else
		PrintTextAbove( who, "No, I can't find anything..." );
	endif

endprogram

function FaceIt( who, what )

	var delta_x := who.x - what.x;
	var delta_y := who.y - what.y;
	var facing  := who.facing;

	if( delta_x < 0 )
		if( delta_y < 0 )
			facing := 3;
		elseif( delta_y > 0 )
			facing := 1;
		else
			facing := 2;
		endif

	elseif( delta_x > 0 )
		if( delta_y < 0 )
			facing := 5;
		elseif( delta_y > 0 )
			facing := 7;
		else
			facing := 6;
		endif

	else
		if( delta_y < 0 )
			facing := 4;
		elseif( delta_y > 0 )
			facing := 0;
		endif
	endif

	who.facing := facing;

endfunction

function EvaluateWeapon(who, what)

	if(!GetObjProperty(what, "IDed"))
		SendSysMessage(who, "That hasn't been identified yet");
		return;
	endif

	var cprops:= GetObjPropertyNames(what);
	if (!len(cprops) || !cprops)
		SendSysMessage(who, "The item isn't particularly interesting");
		return;
	endif

	if(!(GetAttribute( who, ATTRIBUTEID_FORENSICS) >= 130 || who.cmdlevel > 1 || GetObjProperty(who, "IsBard") > 3))
		SendSysMessage(who, "You have no chance of evaluating that item");
		return;
	endif
	
	var chanceofeffect:= GetObjProperty(what, "ChanceOfEffect");
	if (chanceofeffect)
		SendSysMessage(who, "Chance of effect: "+chanceofeffect+"%");
		sleepms(1000);
	endif
	
	var poisonlvl := GetObjProperty(what, "Poisonlvl");
	if (poisonlvl)
		SendSysMessage(who, "Poison level: "+poisonlvl);
		sleepms(1000);
	endif
	
	var lastequipped:= GetObjProperty(what, "LastEquippedBy");
	if (lastequipped && who.cmdlevel > 1)
		SendSysMessage(who, "Last equipped by: "+lastequipped);
	endif
	
	return;
endfunction
	
	
	
		
		