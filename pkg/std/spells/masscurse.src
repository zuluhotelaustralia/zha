use os;
use uo;

include "include/spelldata";
include "include/dotempmods";
include "include/client";

program cast_curse( parms )

	var circle := 6;
	var caster;
	var thetarget;
	var fromhit;

	if (parms[1] == "#MOB")
		caster := parms[2];
		thetarget := parms[3];
		if (parms[4])
			circle := parms[4];
		endif
		if (parms[5])
			fromhit := parms[5];
		endif

	else
		caster := parms;
		thetarget := CanTargetArea(caster, circle);
		if (!thetarget)
			return;
		endif
	endif

	PlaySoundEffect(caster, SFX_SPELL_MASS_CURSE);


	Detach();

	var duration := GetModDuration( caster );

	foreach cast_on in ListMobilesNearLocation( CInt(thetarget.x), CInt(thetarget.y), CInt(thetarget.z), ModifyWithMagicEfficiency( caster, 4 ));

		if ( CheckLineOfSight( caster, cast_on ) )
			var immunity := IsProtected( caster, cast_on, circle );
			if( immunity != IMMUNED )
				//if(!cast_on.criminal)
				//	caster.SetCriminal(1);
				//endif
				if (AutoCallGuardCheck(caster, cast_on))
					return;
				endif
				PlaySoundEffect(cast_on, SFX_SPELL_MASS_CURSE);
            			PlayObjectCenteredEffect( cast_on, FX_SPELL_MASS_CURSE, SPEED_SPELL_MASS_CURSE, LOOP_SPELL_MASS_CURSE);
				send_attack(cast_on,caster);
				var mod_amount := Resisted( caster, cast_on, circle, GetModAmount( caster, 0 ));

				if( immunity == CURSED )
					SendSysMessage( cast_on, "Your equipment increase the effect of the spell!" );
				mod_amount := mod_amount * 2;
				endif

				if (CanMod(cast_on, "all"))
					DoTempMod(cast_on, "all", (0-mod_amount), duration);
				endif
			endif
		endif

	endforeach

endprogram