use uo;
use os;

include "include/spelldata";
include "include/attributes";
include "include/damages";
include "include/res";

const DELAY_BETWEEN_USE	:= 20;

program item_lifestream( who, item )

	if( !ReserveItem( item ) )
		return;
	endif

	var last_use := CInt(GetObjProperty( item, "#last_use" ));
	if( last_use && (last_use > CInt(ReadGameClock() - DELAY_BETWEEN_USE)) )
		SendSysMessage( who, "You must wait before using it again." );
		return;
	endif

	SetObjProperty( item, "#last_use", CInt(ReadGameClock()) );

	if (!can_cast(who, 0)) // (who, circle 0)
		SendSysMessage(who, "You cannot use that here!");
		return;
	endif

	if( !GetObjProperty( item, "RequireNoCharges" ) )
		var charges := CInt(GetObjProperty( item, "charges" ));
		if( !charges )
			PrintTextAbovePrivate( item, "That is out of charges", who );
			return;
		else
			charges := charges - 1;
			SetObjProperty( item, "charges", charges );
		endif
	endif

	SetHp(who, GetMaxHP(who));
	SetMana(who, GetMaxMana(who));
	SetStamina(who, GetMaxStamina(who));

	Detach();

	foreach ghost in ListGhostsNearLocation( who.x, who.y, who.z, 10 );
		if( !(!CheckLineOfSight( who, ghost ) && ghost.multi) )
			ResurrectMobile( ghost, who );
			PlaySoundEffect( who, SFX_SPELL_RESURRECTION );
			PlaySoundEffect( ghost, SFX_SPELL_RESURRECTION );
			SetHp(ghost, GetMaxHP(ghost) );
			SetMana(ghost, GetMaxMana(ghost) );
			SetStamina(ghost, GetMaxStamina(who) );
		endif
	endforeach

	foreach mobile in ListMobilesNearLocation( who.x, who.y, who.z, 10 );
		if ( CheckLineOfSight(who, mobile) && !mobile.multi && !mobile.hidden && !mobile.healed )
			if( GetObjProperty( mobile, "Type" ) == "Undead" )
				ApplyTheDamage( mobile, who, (GetHp(mobile)+3), DMGID_MAGIC );
			else
				SetHp(mobile, GetMaxHP(mobile));
				SetMana(mobile, GetMaxMana(mobile));
				SetStamina(mobile, GetMaxStamina(mobile));
			endif
		endif
	endforeach

	sleep( DELAY_BETWEEN_USE );
	EraseObjProperty( item, "#last_use" );

endprogram